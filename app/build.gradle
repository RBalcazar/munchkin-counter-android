apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

apply from: "$rootProject.projectDir/buildsystem/config.gradle"
apply from: "$rootProject.projectDir/buildsystem/library.gradle"

def configuration = ext.configuration

android {
    compileSdkVersion configuration.compileSdk
    buildToolsVersion configuration.buildTools

    defaultConfig {
        applicationId configuration.applicationId
        minSdkVersion configuration.minimumSdk
        targetSdkVersion configuration.targetSdk
        versionCode buildVersionCode()
        versionName buildVersionName()
        vectorDrawables.useSupportLibrary = true
        resConfigs "en", "de", "es", "it", "fr", "nl", "ru"
        buildConfigField("String", "MIXPANEL_API_KEY", "\"" + MIXPANEL_API_KEY + "\"")
        setProperty("archivesBaseName", "LevelCounterForMunchkin-$versionName")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystores/" + "mnchkn.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            storeFile file(keystoreProperties['key.store'])
            storePassword keystoreProperties['key.store.password']
            keyAlias keystoreProperties['key.alias']
            keyPassword keystoreProperties['key.alias.password']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile "${library.kotlin}"
    compile "${library.support_v4}"
    compile "${library.support_compat}"
    compile "${library.support_design}"
    compile "${library.support_recycler}"
    compile "${library.support_card}"
    compile "${library.constraint_layout}"
    compile "${library.room_runtime}"
    compile "${library.room_rxjava2}"
    kapt "${library.room_compiler}"
    compile "${library.text_drawable}"
    compile "${library.onboarder}"
    compile "${library.mixpanel}"
    compile "${library.mp_android_chart}"
    compile "${library.tap_target_view}"
    compile "${library.rxkotlin}"
    compile "${library.rxandroid}"
    compile "${library.timber}"
    compile "${library.dagger}"
    kapt "${library.dagger_compiler}"
    compile "${library.butterknife}"
    kapt "${library.butterknife_compiler}"
    compile("${library.crashlytics}") {
        transitive = true
    }
    compile("${library.branch}") {
        transitive = true
    }
    compile("${library.fabric}") {
        transitive = true
    }
    testCompile "${library.junit}"
    testCompile "${library.mockito}"
    kaptTest "${library.dagger}"
    androidTestCompile "${library.support_test_runner}"
    androidTestCompile "${library.support_test_rules}"
}